config {
    type: "table",
    schema: "dwh",
    tags: ["fact"],
    bigquery: {
      partitionBy: "order_date",
      clusterBy: ["product_key"]
    }
}

SELECT
  ${keys.surrogate("sod.sales_order_id", "sod.sales_order_detail_id")} AS sales_key,
  ${keys.surrogate("sod.product_id")} AS product_key,
  ${keys.surrogate("customer_id")} AS customer_key,
  ${keys.surrogate("credit_card_id")} AS credit_card_key,
  ${keys.surrogate("ship_to_address_id")} AS ship_address_key,
  ${keys.surrogate("status")} AS order_status_key,
  ${keys.surrogate("order_date")} AS order_date_key,
  soh.order_date,
  sod.unit_price,
  sod.unit_price_discount,
  p.standard_cost AS cost_of_goods_sold,
  sod.order_qty AS order_quantity,
  sod.order_qty * sod.unit_price AS gross_revenue,
  (sod.order_qty * sod.unit_price * (1 - sod.unit_price_discount)) - (p.standard_cost) AS gross_profit
FROM
  ${ref("stg_sales_order_detail")} sod,
  ${ref("stg_sales_order_header")} soh,
  ${ref("stg_product")} p
WHERE
  sod.sales_order_id = soh.sales_order_id
  AND sod.product_id = p.product_id

post_operations {
  ${keys.primary(ctx, "sales_key")}
  ${keys.foreign(ctx, ref("dim_product"), "product_key")}
  ${keys.foreign(ctx, ref("dim_customer"), "customer_key")}
  ${keys.foreign(ctx, ref("dim_credit_card"), "credit_card_key")}
  ${keys.foreign(ctx, ref("dim_address"), "address_key", "ship_address_key")}
  ${keys.foreign(ctx, ref("dim_order_status"), "order_status_key")}
  ${keys.foreign(ctx, ref("dim_date"), "date_key", "order_date_key")}
}