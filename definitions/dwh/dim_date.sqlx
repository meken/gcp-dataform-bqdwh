config {
    type: "table",
    tags: ["dimension"]
}
SELECT
  ${keys.surrogate("FORMAT_DATE('%F', d)")} as date_key,
  d AS full_date,
  EXTRACT(YEAR FROM d) AS year,
  EXTRACT(MONTH FROM d) AS month,
  EXTRACT(DAY FROM d) AS day,
  EXTRACT(WEEK FROM d) AS year_week,
  EXTRACT(DAYOFYEAR FROM d) AS year_day,
  LAST_DAY(d) AS last_day,
  FORMAT_DATE('%Q', d) as fiscal_qtr,
  FORMAT_DATE('%B', d) as month_name,
  FORMAT_DATE('%w', d) AS week_day,
  FORMAT_DATE('%A', d) AS day_name,
  (CASE WHEN FORMAT_DATE('%A', d) IN ('Sunday', 'Saturday') THEN 0 ELSE 1 END) AS day_is_weekday,
FROM (
  SELECT
    *
  FROM
    UNNEST(GENERATE_DATE_ARRAY('2001-01-01', '2030-01-01', INTERVAL 1 DAY)) AS d )