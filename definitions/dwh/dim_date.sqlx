config {
    type: "table",
    schema: "dwh",
    tags: ["dimension"]
}
SELECT
  ${keys.surrogate("FORMAT_DATE('%F', d)")} as date_key,
  d AS full_date,
  EXTRACT(YEAR FROM d) AS year,
  EXTRACT(MONTH FROM d) AS month,
  EXTRACT(DAY FROM d) AS day,
  EXTRACT(WEEK FROM d) AS week_of_year,
  EXTRACT(DAYOFYEAR FROM d) AS day_of_year,
  EXTRACT(QUARTER FROM d) AS quarter_of_year,
  EXTRACT(DAYOFWEEK FROM d) AS day_of_week,
  FORMAT_DATE('%B', d) AS month_name,
  FORMAT_DATE('%A', d) AS day_name,
  (LAST_DAY(d) = d) AS is_last_day_of_month,
  (EXTRACT(DAYOFWEEK FROM d) IN (1, 7)) is_weekend
FROM (
  SELECT
    *
  FROM
    UNNEST(GENERATE_DATE_ARRAY('2001-01-01', '2030-01-01', INTERVAL 1 DAY)) AS d 
)

post_operations {
  ${keys.primary(ctx, "date_key")}
}